# This is the Pegex grammar for Pegex grammars!

grammar:
    [
        <comment>*
        <rule_definition>
    ]+
    <comment>*

rule_definition:
    /<WS>*/
    <rule_name> /<BLANK>*<COLON><WS>*/
    <rule_group>
    <rule_ending>

rule_group: <any_group> | <all_group>

all_group:
    <rule_item>
    [ /<WS>*/ <rule_item> ]*

any_group:
    <rule_item>
    [ /<WS>*<PIPE><WS>*/ <rule_item> ]+

rule_item:
    <rule_reference> |
    <regular_expression> |
    <bracketed_group> |
    <error_message>

rule_reference:
    /(<rule_modifier>?)<LANGLE><rule_name><RANGLE>(<rule_quantifier>?)/

regular_expression: /<SLASH>([^<SLASH><DOS>]*)<SLASH>/

bracketed_group:
    /<LSQUARE><WS>*/
    <rule_group>
    /<WS>*<RSQUARE>(<rule_quantifier>?)/

error_message: /<GRAVE>([^<GRAVE><DOS>]*)<GRAVE>/

rule_modifier: /[<BANG><EQUAL>]/

rule_quantifier: /[<STAR><PLUS><QMARK>]/

rule_name: /(<ALPHA><WORD>*)/

rule_ending: /<WS>*?(?:<BREAK><WS>*|<SEMI><WS>*|<EOS>)/

comment: /(?:<blank_line>|<comment_line>)/

blank_line: /<BLANK>*<EOL>/

comment_line: /<HASH><line>/

line: /<ANY>*<EOL>/

