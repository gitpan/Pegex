# This is the Pegex grammar for Pegex grammars!

grammar: .<comment>* <rule_definition>+ %% .<comment>*

rule_definition:
    /~/
    <rule_name> /<BLANK>*<COLON>~/
    <rule_group>
    <rule_ending>

rule_group: <any_group> | <all_group>

all_group: <rule_part>+ % /~/

any_group: <rule_part>2+ % /~<PIPE>~/

rule_part: <rule_item>1-2 % /~<WS>(<PERCENT><PERCENT>?)<WS>~/

rule_item:
    <rule_reference> |
    <regular_expression> |
    <bracketed_group> |
    <error_message>

rule_reference:
    /(<rule_modifier>?)<LANGLE><rule_name><RANGLE>(<rule_quantifier>?)/

regular_expression: /<SLASH>([^<SLASH><DOS>]*)<SLASH>/

bracketed_group:
    /(<group_modifier>?)<LSQUARE>~/
    <rule_group>
    /~<RSQUARE>(<rule_quantifier>?)/

error_message: /<GRAVE>([^<GRAVE><DOS>]*)<GRAVE>/

rule_modifier: /[<BANG><EQUAL><PLUS><DASH><DOT>]/

group_modifier: /[<DOT>]/

rule_quantifier: /(?:[<STAR><PLUS><QMARK>]|<DIGIT>+(?:<DASH><DIGIT>+|<PLUS>)?)/

rule_name: /(<ALPHA><WORD>*)/

rule_ending: /~?(?:<BREAK>~|<SEMI>~|<EOS>)/

comment: /(?:<blank_line>|<comment_line>)/

blank_line: /<BLANK>*<EOL>/

comment_line: /<HASH><line>/

line: /<ANY>*<EOL>/
